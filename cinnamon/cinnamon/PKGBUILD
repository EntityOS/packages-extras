# $Id: PKGBUILD 131113 2015-04-10 15:12:41Z faidoc $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=2.8.8
pkgrel=1.288
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'cinnamon-settings-daemon' 'cinnamon-session'
         'cinnamon-translations' 'cjs' 'clutter-gtk' 'gnome-backgrounds' 'gconf'
         'gnome-themes-standard' 'gstreamer' 'libgnome-keyring' 'librsvg'
         'networkmanager' 'muffin' 'python2-cairo' 'python2-dbus' 'python2-pillow'
         'python2-pam' 'python2-pexpect' 'python2-pyinotify' 'python2-lxml' 'webkitgtk'
         'cinnamon-control-center' 'cinnamon-screensaver' 'cinnamon-menus' 'libgnomekbd'
         'network-manager-applet' 'nemo' 'polkit-gnome' 'python2-ptyprocess')
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'gobject-introspection')
options=('!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/archive/$pkgver.tar.gz"
        "set_wheel.patch"
        "default-theme.patch"
        "fix-missing-icons.patch")
sha256sums=('4ae7ed062e2bfacabdac66002c14666c4709182d45a6f0140a577800e28b0326'
            '9f1d4f6b3391f6ff0a6761fa4bcfab297a5e1185a3d8ff24a760284b667366e2'
            '566585873f38a79ec248b916645a2e081abec3c6d4df2c34339cde1f35375cc5')
_allowed_in="staging"

prepare() {
  cd ${srcdir}/Cinnamon*

  # Use wheel group instread of sudo
  patch -Np1 -i ../set_wheel.patch

  # Set default theme to 'cinnamon'
  patch -Np1 -i ../default-theme.patch
  
  # Fix missing icons with Adwaita theme
  patch -Np1 -i ../fix-missing-icons.patch
  
  # This should be submitted upstream. It has nothing to do with Cinnamon working on Arch.
  # Don't hardcode Nemo
  # patch -Np1 -i ../dont-hardcode-nemo.patch
  
  # Python2 fix
  #sed -i 's:/usr/bin/python :/usr/bin/python2 :' files/usr/bin/cinnamon-menu-editor
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Add polkit agent to required components
  sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
    files/usr/share/cinnamon-session/sessions/cinnamon*.session

  # Use pkexec instead of gksu
  sed -i 's/gksu/pkexec/' files/usr/bin/cinnamon-settings-users

  # Check for the cc-panel path, not for the unneeded binary
  #sed -i 's|/usr/bin/cinnamon-control-center|/usr/lib/cinnamon-control-center-1/panels|' \
  #  files/usr/bin/cinnamon-settings

  # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
  #sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
  #  files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py

  # Fix selected background color in Cinnamon Settings for Adwaita theme
  sed -i 's/@selected_bg_color;/@theme_selected_bg_color;/' \
    files/usr/share/cinnamon/cinnamon-settings/cinnamon-settings.py
}

build() {
  cd ${srcdir}/Cinnamon*

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes
  
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  #sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  
  make
}

package() {
  cd ${srcdir}/Cinnamon*
  make DESTDIR="${pkgdir}" install
}
 

