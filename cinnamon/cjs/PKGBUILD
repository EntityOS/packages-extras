# Maintainer: Risha S Chagnmai <michaelspeed.michaelcorp@live.com>

pkgname=cjs
pkgver=2.8.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cjs"
license=(GPL)
depends=(cairo gobject-introspection-runtime js dbus-glib)
makedepends=(gnome-common python2 gobject-introspection)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cjs/archive/${pkgver}.tar.gz")
sha256sums=('eceddb46dd7c1da9652c99355158feb8a107e213b5f7240700e1777b28d00a73')
_allowed_in="staging"
build() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's|#! /usr/bin/env python|#! /usr/bin/env python2|' \
  		verbump.py

  ./autogen.sh --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
